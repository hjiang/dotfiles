# -*- mode: shell-script -*-

if [[ -o interactive && -z "$PIPENV_ACTIVE" ]] && type keychain > /dev/null
then
    ssh_identities=$(for file in $HOME/.ssh/*.pub; do echo "${file%%.pub}"; done)
    eval "$(keychain --eval --quiet -Q $ssh_identities)"
fi

if [[ -o interactive && -z "$PIPENV_ACTIVE" && "${TERM}" != "dumb" ]] && type starship > /dev/null
then
    eval "$(starship init zsh)"
fi

if [[ "$(uname)" = 'Darwin' ]] && [[ "$http_proxy" = "" ]] && \
    [[ "$https_proxy" = "" ]] && [[ "$all_proxy" = "" ]] && \
    (type macos-proxy-env >> /dev/null) && [[ -o interactive ]]
then
    eval $(macos-proxy-env)
fi

vterm_printf(){
    if [ -n "$TMUX" ] && ([ "${TERM%%-*}" = "tmux" ] || [ "${TERM%%-*}" = "screen" ] ); then
        # Tell tmux to pass the escape sequences through
        printf "\ePtmux;\e\e]%s\007\e\\" "$1"
    elif [ "${TERM%%-*}" = "screen" ]; then
        # GNU screen (screen, screen-256color, screen-256color-bce)
        printf "\eP\e]%s\007\e\\" "$1"
    else
        printf "\e]%s\e\\" "$1"
    fi
}

if [[ "$INSIDE_EMACS" = 'vterm' ]]; then
    alias clear='vterm_printf "51;Evterm-clear-scrollback";tput clear'
fi

export HOMEBREW_NO_ENV_HINTS=1

if [[ -e "/Applications/Surge.app/Contents/Applications/surge-cli" ]]; then
    alias surge-cli="/Applications/Surge.app/Contents/Applications/surge-cli"
fi

if [[ -e "/Applications/Tailscale.app/Contents/MacOS/Tailscale" ]]; then
    alias tailscale="/Applications/Tailscale.app/Contents/MacOS/Tailscale"
fi

fpath+=~/.zfunc
autoload -Uz compinit && compinit

# Some zsh plugins such as poetry expects this directory to exist
if [[ ! -d "$ZSH_CACHE_DIR"/completions ]]; then
    mkdir -p "$ZSH_CACHE_DIR"/completions
fi

if (( $+commands[eza] )); then
    alias ls='eza'
fi

if (( $+commands[zoxide] )); then
    eval "$(zoxide init zsh)"
    alias cd="z"
fi

if [[ -a /usr/share/fzf/key-bindings.zsh ]]; then
    source /usr/share/fzf/key-bindings.zsh
fi

if [[ -a /usr/share/zsh/site-functions/fzf-tab.zsh ]]; then
    source /usr/share/zsh/site-functions/fzf-tab.zsh
fi

if [[ ! -d $ZSH_CACHE_DIR/antidote ]]; then
    git clone --depth=1 https://github.com/mattmc3/antidote.git $ZSH_CACHE_DIR/antidote
fi

source $ZSH_CACHE_DIR/antidote/antidote.zsh
antidote load

alias gcam='git commit --all --message'
alias gp='git push'
# Unset the API key, so that claude uses the subscription
alias claude='env ANTHROPIC_API_KEY= claude'


[[ -f "$HOME/.config/zsh/local.zsh" ]] && source "$HOME/.config/zsh/local.zsh"

if command -v gen-envel >/dev/null 2>&1; then
    gen-envel --vars PATH http_proxy https_proxy all_proxy SSH_AUTH_SOCK \
              SSH_AGENT_PID --output ${XDG_CACHE_HOME}/emacs/env.el
fi
